//------------------------------------------------------------------------------
//
// Parameters defining domain extentss and resolution
// (This should be fiddled with, but with care)
//
//------------------------------------------------------------------------------
option = 3;  // 1 - fuel; 2 - cladding; 3 - water

r_ilay = 0.007;
r_fuel = 0.008;
r_clad = 0.01;   // 1   cm
r_blay = 0.012;  // 1.2 cm
h_full = 0.02;   // 2   cm
x_box  = 0.06;
y_box  = 0.24;

delta = 2.0e-9;  // make it 2.0e-4 to see something

n_ilay =  5;  // number of nodes in inner layer
n_fuel = 13;  // number of nodes across the cladding
n_blay = 13;  // number of layers in the boundary layer
n_azim = 41;  // number of nodes in azimuthal direction
n_axis = 21;  // number of nodes in axial direction

prog = 1.2;

x_cent = 0.0;
y_cent = 0.0;

//------------------------------------------------------------------------------
//
// Parameters for the problem definition algorithms
// (These are better kept untouched)
//
//------------------------------------------------------------------------------
TINY = 1.0e-9;
HUGE = 1.0e+9;

// Work out necessary resolutions in the fluid
circum = 2.0 * r_blay * Pi;
delta_min = circum / 4.0 / (n_azim - 1);
delta_max = delta_min * 3.0;

// Lines in radial direction
LINES   = 100;
CIRCLES = 200;

//------------------------------------------------------------------------------
//
// Points
//
//------------------------------------------------------------------------------

// Points defining the arcs
For lay In{ 0 : 5 }
  For n In{ 1 : 4 }
    point = n + lay*4;
    If(lay == 0) r = r_ilay;         EndIf
    If(lay == 1) r = r_fuel - delta; EndIf
    If(lay == 2) r = r_fuel + delta; EndIf
    If(lay == 3) r = r_clad - delta; EndIf
    If(lay == 4) r = r_clad + delta; EndIf
    If(lay == 5) r = r_blay;         EndIf
    angle = Pi/4.0 + (n-1) * Pi/2.0;
    Point(point) = {x_cent + r * Cos(angle), y_cent + r * Sin(angle), 0.0};
  EndFor
EndFor

// Central point
CENTRAL = point + 1;
Point(CENTRAL) = {x_cent, y_cent, 0.0};

// Points defining the domain
Point(CENTRAL+1) = {x_cent + x_box/2.0, y_cent + y_box - x_box/2.0, 0.0};
Point(CENTRAL+2) = {x_cent - x_box/2.0, y_cent + y_box - x_box/2.0, 0.0};
Point(CENTRAL+3) = {x_cent - x_box/2.0, y_cent - x_box/2.0, 0.0};
Point(CENTRAL+4) = {x_cent + x_box/2.0, y_cent - x_box/2.0, 0.0};
Printf("Defined: %g points", CENTRAL+4);

//------------------------------------------------------------------------------
//
// Lines
//
//------------------------------------------------------------------------------

// Lines in all layers, radial direction, total 12 of them
For lay In{ 0 : 2 }     // 0 - inner layer; 1 - cladding; 2 - boundary layer
  For line In{ 1 : 4 }
    n1 = line     + lay*4;
    n2 = line + 4 + lay*4;
    If(lay > 0) n1 = n1 + 4; n2 = n2 + 4; EndIf
    If(lay > 1) n1 = n1 + 4; n2 = n2 + 4; EndIf
    Line(LINES + line + lay*4) = {n1, n2};
 EndFor
EndFor
Transfinite Line{LINES +1:LINES +4} = n_ilay;
Transfinite Line{LINES +5:LINES +8} = n_fuel Using Progression 1.0/prog;
Transfinite Line{LINES +9:LINES+12} = n_blay Using Progression prog;

// Lines defining the bounding box
Line(LINES + 13) = {26, 27};
Line(LINES + 14) = {27, 28};
Line(LINES + 15) = {28, 29};
Line(LINES + 16) = {29, 26};

// All the circles
For lay In{ 0 : 5 }     // 0 - inner layer; 1 - cladding; 2 - boundary layer
  For circ In{ 1 : 4 }
    n1 = circ     + lay*4;
    n2 = circ + 1 + lay*4;
    If(circ == 4) n2 = n2 - 4; EndIf
    Circle(CIRCLES + circ + lay*4) = {n1, CENTRAL, n2};
  EndFor
EndFor
Transfinite Line{CIRCLES+1:CIRCLES+24} = n_azim;

//------------------------------------------------------------------------------
//
// Surfaces
//
//------------------------------------------------------------------------------

// Create surface in layer around fuel cladding
For lay In{ 0 : 2 }     // 0 - inner layer; 1 - cladding; 2 - boundary layer
  For loop In{ 1 : 4 }
    l1 = LINES   + loop + lay*4;   l3 = l1 + 1;
    c4 = CIRCLES + loop + lay*4;   c2 = c4 + 4;
    If(loop == 4) l3 = l3 - 4; EndIf
    If(lay > 0) c4 = c4 + 4; c2 = c2 + 4; EndIf
    If(lay > 1) c4 = c4 + 4; c2 = c2 + 4; EndIf
    Curve Loop   (loop + lay*4) = {l1, c2, -l3, -c4};
    Plane Surface(loop + lay*4) = {loop + lay*4};
  EndFor
EndFor
Transfinite Surface  "*";

// Create surface for the fluid domain
SURF_FUEL = 13;
Curve Loop   (SURF_FUEL) = {CIRCLES+1:CIRCLES+4};
Plane Surface(SURF_FUEL) = {SURF_FUEL};

SURF_WATER = 14;
Curve Loop(SURF_WATER)   = {LINES  +13:LINES  +16};
Curve Loop(SURF_WATER+1) = {CIRCLES+21:CIRCLES+24};
Plane Surface(14) = {SURF_WATER, SURF_WATER+1};

// Recombine all surfaces
// This is mandatory for extrusion <----------= IMPORTANT
Recombine Surface    "*";

// Define resolution in the fluid domain
// How come it only impacts the fluid domain?
Field[2]         = Cylinder;
Field[2].VIn     = delta_min;
Field[2].VOut    = delta_max;
Field[2].Radius  = r_blay * 1.2;
Field[2].XCenter = x_cent;
Field[2].YCenter = y_cent;
Background Field = 2;

Mesh.Algorithm = 8;

//------------------------------------------------------------------------------
// Volumes
//------------------------------------------------------------------------------

// Fuel
If(option == 1)
  Extrude {0, 0, h_full} {
    Surface{1:4, 13};
    Layers{n_axis};
    Recombine;
  }
  Physical Surface("fuel_wall") = {237, 259, 281, 303};
  Physical Volume("fuel")       = {1:5};
  Recursive Delete {Surface{5:12,14};}

// Cladding
ElseIf(option == 2)
  Extrude {0, 0, h_full} {
    Surface{5:8};
    Layers{n_axis};
    Recombine;
  }
  Physical Surface("cladding_inner_wall") = {245, 267, 289, 311};
  Physical Surface("cladding_outer_wall") = {237, 259, 281, 303};
  Physical Volume("cladding") = {1:4};
  Recursive Delete {Surface{1:4, 9:14};}

// Water
ElseIf(option == 3)
  Extrude {0, 0, h_full} {
    Surface{9:12,14};
    Layers{n_axis};
    Recombine;
  }

  Physical Surface("cylinder_wall") = {311, 245, 267, 289};
  Physical Surface("sides_x")       = {329, 337};
  Physical Surface("bottom")        = {333};
  Physical Surface("top")           = {325};
  Physical Volume("water")          = {1:5};
  Recursive Delete {Surface{1:8,13};}

EndIf

// Periodic direction is the same for all options
Physical Surface("sides_z")
  = {Surface In BoundingBox{-HUGE, -HUGE, h_full-TINY,
                            +HUGE, +HUGE, h_full+TINY},
     Surface In BoundingBox{-HUGE, -HUGE, 0.0   -TINY,
                            +HUGE, +HUGE, 0.0   +TINY}};
